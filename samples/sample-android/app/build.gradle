apply plugin: 'com.google.osdetector'



ext.removeUnusedResourcesV2Enable = true

import com.google.gson.Gson
import com.google.gson.JsonArray

import com.tencent.matrix.plugin.compat.AgpCompat

apply plugin: 'com.android.application'


apply plugin: 'plugin.resTools' // 资源重命名插件
// 配置插件dsl
resConfig {
    new_prefix = 'better_' // 资源前缀
    old_prefix = '' // 老前缀，可为''空字符串
// === below use default
// resFolderPath 资源目录
// srcFolderPath 源代码目录
// manifestFilePath 清单文件目录
}

configurations.all {
    // check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    signingConfigs {
        debug {
            storeFile file("./keystore/debug.keystore")
        }
        release {
            storeFile file("./keystore/debug.keystore")
        }
    }


    defaultConfig {
        applicationId "sample.tencent.matrix"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        flavorDimensions "versionCode"
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            minifyEnabled true      // 混淆打开
            shrinkResources true   // 将无用的类资源过滤，Gradle2.2版本后设置为false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    packagingOptions {
        pickFirst 'lib/armeabi-v7a/libc++_shared.so'
        pickFirst 'lib/arm64-v8a/libc++_shared.so'
        pickFirst 'lib/armeabi-v7a/libwechatbacktrace.so'
        pickFirst 'lib/arm64-v8a/libwechatbacktrace.so'
    }

    // Encapsulates your external native build configurations.
    externalNativeBuild {

        // Encapsulates your CMake build configurations.
        cmake {

            // Provides a relative path to your CMake build script.
            path "CMakeLists.txt"
        }
    }

    sourceSets {
        main {
            jniLibs {
                setSrcDirs (['./libs'] as Set)
            }
        }

    }

    //1.启用java8
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    if (!removeUnusedResourcesV2Enable) {
        applicationVariants.all { variant ->
            if (variant.name.equalsIgnoreCase("debug")) {
                packageDebug.doLast {
                    ProcessBuilder processBuilder = new ProcessBuilder();
                    println configurations.apkCheckerDependency.getAt(0).getAbsolutePath()
                    processBuilder.command("java",
                            "-jar", configurations.apkCheckerDependency.getAt(0).getAbsolutePath(),
                            "--apk", variant.outputs.first().outputFile.getAbsolutePath(),
                            "--output", project.getProjectDir().getAbsolutePath() + "/unused_resources",
                            "--format", "json",
                            "-unusedResources", "--rTxt", project.getBuildDir().getAbsolutePath() + "/intermediates/${AgpCompat.getIntermediatesSymbolDirName.invoke()}/${variant.name}/R.txt");
                    Process process = processBuilder.start();
                    process.waitFor()
                    println process.inputStream.text
                    println process.errorStream.text
                    File outputFile = new File(project.getProjectDir().getAbsolutePath() + "/unused_resources.json");
                    if (outputFile.exists()) {
                        Gson gson = new Gson();
                        JsonArray jsonArray = gson.fromJson(outputFile.text, JsonArray.class);
                        for (int i = 0; i < jsonArray.size(); i++) {
                            if (jsonArray.get(i).asJsonObject.get("taskType").asInt == 12) {
                                JsonArray resList = jsonArray.get(i).asJsonObject.get("unused-resources").asJsonArray;
                                for (int j = 0; j < resList.size(); j++) {
                                    project.ext.unusedResourcesSet.add(resList.get(j).asString);
                                }
                                println "find unused resources:\n" + unusedResourcesSet
                                break;
                            }
                        }
                        outputFile.delete();
                    }
                }
            }
        }
    }

    productFlavors {
        dev {
            //versionName
            versionName "1.0.0"
            //versionCode
            versionCode 1

        }
    }
}

configurations {
    apkCheckerDependency
    sevenZipDependency
}

if (useLocalMaven()) {
    apply from: project.file('dependencies-src.gradle')
} else {
    apply from: project.file('dependencies-aar.gradle')
}

if (!removeUnusedResourcesV2Enable) {
    project.ext.unusedResourcesSet = new HashSet<String>()
}

apply plugin: 'com.tencent.matrix-plugin'
matrix {

    logLevel "D"
    // LeakCanary needs to know which variants have obfuscation turned on
    filterObfuscatedVariants { variant ->
        variant.name == "devDebug"
    }
    trace {
        enable = true
        baseMethodMapFile = "${project.projectDir}/matrixTrace/methodMapping.txt"
        blackListFile = "${project.projectDir}/matrixTrace/blackMethodList.txt"
        //默认transform: transformClassesWithDexBuilder, transformClassesWithDex
//        customDexTransformName = "transformClassesAndResourcesWithR8"
    }
    removeUnusedResources {
//        variant = "debug"
//
//        v2 = removeUnusedResourcesV2Enable
//
//        if (!v2) {
//            unusedResources = project.ext.unusedResourcesSet
//        }

        enable false
//        needSign true
//        shrinkArsc true
//        shrinkDuplicates true
//        use7zip = true
//        zipAlign = true
//        embedResGuard true
//
//        apkCheckerPath = "${project.configurations.apkCheckerDependency.resolve().find { it.name.startsWith("matrix-apk-canary") }.getAbsolutePath()}"
//        sevenZipPath = "${project.configurations.sevenZipDependency.resolve().getAt(0).getAbsolutePath()}"
//        //Notice: You need to modify the  value of $apksignerPath on different platform. the value below only suitable for Mac Platform,
//        //if on Windows, you may have to  replace apksigner with apksigner.bat.
//        apksignerPath = "${android.getSdkDirectory().getAbsolutePath()}/build-tools/${android.getBuildToolsVersion()}/apksigner"
//        zipAlignPath = "${android.getSdkDirectory().getAbsolutePath()}/build-tools/${android.getBuildToolsVersion()}/zipalign"
//        ignoreResources = ["R.id.*", "R.bool.*", "R.layout.unused_layout"]
    }
}

project.tasks.whenTaskAdded {
    if (it.name?.equals("assembleDebug") || it.name?.equals("assembleRelease")) {
        it.dependsOn "cleanBuildDirTask".with { taskName ->
            if (tasks.findByName(taskName)) {
                return tasks.findByName(taskName)
            }
            return task(taskName) {
                doFirst {
                    logger.lifecycle "Clean app bulid dir before assemble"
                    project.file(project.buildDir).deleteDir()
                }
            }
        }
    }
}
